10  GOTO 1010
20  PRINT  CHR$(4);"UNLOCK RC1000.SYS": PRINT  CHR$(4);"SAVE RC1000.SYS": PRINT  CHR$(4);"LOCK RC1000.SYS"
30  END 
50  REM  ***********************
51  REM  **RC1000.SYS V1.00 AP**
52  REM  ***********************
53  REM  
54  REM     BY H.C. PENNINGTON
55  REM     APPLE VERSION
56  REM     
57  REM     COPYRIGHT (C) 1984
58  REM     HATTORI CORPOR@SION
59  REM     OF AMERICA
60  REM  
61  REM     1330 WEST WALNUT
62  REM     PARKWAY
63  REM     COMPTON, CA  90220
64  REM  
65  REM      REV:012885
1000  REM   
1001  REM   INIT<CTRL-J>
1010  SPEED= 255: HOME : NORMAL : PRINT  CHR$(17)
1011  POKE 222,0: GOSUB 19010
1015  FOR X = 1 TO 36:SP$ = SP$ +" ": NEXT 
1016 PAD$ = "                           "
1030  FOR X = 0 TO 3:LA(X) = 0: NEXT X
1050 EF = 80:MU = 0:LN = 1:CL = 0:LA = 0
1070 F1 = 0:F2 = 0:F3 = 0:F5 = 0:CC = 32
1075 D$ =  CHR$(13) + CHR$(4):N$ =  CHR$(4)
1076  PRINT D$;"NOMON C,I,O": REM <CTRL-J>
1080  FOR X = 0 TO 17
1082  READ IP: POKE 768 +X,IP
1084  NEXT X
1086  DATA  104,168,104,166,223,154,72,152,72,96,00,00,169,00,108,54,00,00: REM  <CTRL-J>
1092 WT = 0:WF = 157: REM <CTRL-J>
1130 PZ = 1236:PO = 0:IL = 23:EN = 80
1132 YEAR$ = "01/01/84"
1133 HS = 1
1134 SP$ = "                                    "
1155  DIM DA$(81),LN$(81),MG$(35),SD(81)
1160  DIM SN(81),MU$(40),DT(81),DA(81)
1170 TZ = 4: REM <CTRL-J>
1171  GOSUB 15500 REM --VARINIT
1172  GOSUB 15010 REM --DRAWSCN
1173  GOSUB 10130 REM --MAKEFILE
1174  GOSUB 5710
1200  FOR X = 0 TO 27:CM$(0) = CM$(0) + CHR$(X): NEXT :CM$(0) = CM$(0) + CHR$(127):CM(0) =  LEN(CM$(0))
1201 CM$(1) = ";!@$%()":CM(1) =  LEN(CM$(1))
1202 VX$ = " 1234567890:*/#&+-=?.":VX =  LEN(VX$): REM <CTRL-J>
1210  REM 
1220  REM 
1230  REM 
1240 YR =  -1:INIT = 0: GOTO 10400
1250  REM 
1299  REM <CTRL-J>
2000  REM 
2001  REM  MAIN MENU<CTRL-J>
2010 MU = 0: GOSUB 3340
2040 MG = 11: GOSUB 13430: GOSUB 3540
2061  IF IK$ =  CHR$(27)  THEN 3700
2070  ON  VAL(IK$) GOTO 3700,2440,2640,2940,3230
2090  GOTO 2010: REM <CTRL-J>
2400  REM 
2401  REM  XMIT TO WATCH<CTRL-J>
2440 MU = 3: GOSUB 13630: GOSUB 3340
2445  VTAB 9: HTAB 19: PRINT " <CTRL-H> ";
2450  GET IK$: IF IK$ =  CHR$(27)  THEN 2010
2460  IF IK$ < > CHR$(13)  THEN 2450
2470  GOSUB 7600: PRINT  CHR$(17): GOSUB 13600
2485  GOSUB 15010: GOSUB 5710: GOTO 2010: REM <CTRL-J>
2600  REM 
2601  REM PRNT WTCH FILE<CTRL-J>
2640 MG = 22: GOSUB 13430:VT =  PEEK(37) +1:HT =  PEEK(36)
2660  GET IK$: IF IK$ = ""  THEN  GOTO 2663
2661  IF IK$ =  CHR$(27)  THEN 2010
2662  IF IK$ =  CHR$(13)  THEN 2700
2663  VTAB (VT): HTAB HT: PRINT  CHR$(7);: GOTO 2660: REM <CTRL-J>
2700 TB = 4: PR# 1
2705  PRINT  LEFT$(SP$,45 -( LEN(MG$(24) +FI$)/2)) +MG$(24) +FI$
2710  PRINT  SPC( TB);: FOR X = 1 TO 70: PRINT "=";: NEXT 
2720  PRINT  CHR$(13)
2730 X = 1
2750  IF X < = EN  THEN PR$ =  LEFT$(SP$,TB) +LN$(X) +">" +DA$(X) +"<"
2751  IF X < = EN  THEN PR$ = PR$ +PR$(DA(X)) +PR$(DT(X))
2760  IF X +40 < = EN  THEN PR$ = PR$ + LEFT$(SP$,3) +LN$(X +40) +">"
2761  IF X +40 < = EN  THEN PR$ = PR$ +DA$(X +40) +"<" +PR$(DA(X +40)) +PR$(DT(X +40))
2775  IF X >EN  THEN 2810
2780  PRINT PR$
2800 X = X +1: IF X <41  THEN 2750
2810  FOR Z = 1 TO 63 -X: PRINT " ": NEXT Z
2830  PR# 0: GOSUB 15010: GOSUB 5710: GOTO 2010: REM <CTRL-J>
2900  REM 
2901  REM  SYSTEM MENU<CTRL-J>
2940 MU = 1: GOSUB 3340
2950 MG = 11: GOSUB 13430: GOSUB 3540
2970  IF IK$ =  CHR$(27)  THEN  GOTO 2010
2980  ON IK GOTO 11530,10330,3030,3040,3140
3000  GOTO 2950
3020 MU = 0
3030  GOSUB 9070: GOTO 2940
3040  GOSUB 9330: GOTO 2940: REM <CTRL-J>
3100  REM 
3101  REM  SET TIME ZONE<CTRL-J>
3140  GOSUB 13630: GOSUB 13930
3150  VTAB (9): HTAB 3: PRINT "TIME ZONE:"
3160 IP$ = "": GOSUB 14510: IF IP$ = ""  THEN 2950
3170  IF  VAL(IP$) >24  OR  VAL(IP$) <1  THEN MG = 20: GOSUB 13230: GOTO 3140
3180 TZ =  VAL(IP$): GOSUB 11330: GOTO 2950
3200  GOTO 2010: REM <CTRL-J>
3220  REM 
3221  REM  EXIT PROGRAM<CTRL-J>
3230 MG = 14: GOSUB 13430
3240  GET IK$: IF IK$ = ""  THEN 3240
3245  GOSUB 13630
3250  IF IK$ = "N"  THEN 2010
3260  IF IK$ < >"Y"  THEN 3240
3270  PRINT D$;"CLOSE"
3280  HOME : NEW : REM <CTRL-J>
3300  REM 
3301  REM  PRINT MENUS<CTRL-J>
3340 MU = MU *6
3360  FOR X = 0 TO 5: VTAB (2 +X): HTAB 3: PRINT MU$(MU +X);: NEXT X
3400 MU = MU *.1: RETURN : REM <CTRL-J>
3500  REM 
3501  REM  MENU INPUT<CTRL-J>
3540  VTAB (9): HTAB 7: PRINT " <CTRL-H> ";: GET IK$: IF IK$ = ""  THEN  GOTO 3540
3570  IF IK$ =  CHR$(27)  THEN  RETURN 
3580 IK =  VAL(IK$): IF IK <1  OR IK >5  THEN  PRINT  CHR$(7);: GOTO 3540
3590  RETURN : REM <CTRL-J>
3700  REM 
3701  REM  EDIT/KEYBOARD SCAN<CTRL-J>
3750 PO = 0:IL = 11: IF DT(LN) = 0  OR DA(LN) = 5  THEN IL = 23
3760  VTAB (20): HTAB 31: PRINT MG$(F1)
3765  ON HS GOSUB 15205,15305,15405,15463: REM <CTRL-J>
3780  VTAB (18): HTAB 6 +PO: PRINT "<CTRL-H>";
3782  GET IK$: IF IK$ = ""  THEN 3798
3785 IK =  ASC(IK$): REM <CTRL-J>
3790  IF IK = 59  THEN 3895
3791  IF IK <28  THEN 3870
3792  IF IK <65  THEN 3800
3793  IF IK <91  THEN 3840
3794  IF IK <123  THEN IK$ =  CHR$(IK -32): GOTO 3800
3796  IF IK <128  THEN 3870
3798  PRINT  CHR$(7);: POKE 49152,0: GOTO 3780: REM <CTRL-J>
3800  IF  ASC(IK$) <65  THEN  FOR X = 1 TO VX +CM(1): IF IK$ < > MID$ (VX$ +CM$(1),X,1)  THEN  NEXT : GOTO 3798: REM <CTRL-J>
3820  IF X >21  THEN X = X -21: GOTO 3865
3840  IF F1 >0  THEN 4555
3850  VTAB (18): HTAB 5 +PO: PRINT IK$;:F2 = 1
3852 PO = PO +1: IF PO >IL  THEN PO = 0: GOSUB 4780: VTAB (18): HTAB 5 +PO
3854  IF PO = 0  THEN  PRINT  CHR$( PEEK(PZ +PO) -128) + CHR$(8);
3856  GOTO 3780: REM <CTRL-J>
3859  REM  SHIFT FUNCTIONS<CTRL-J>
3865  ON X GOTO 3895,12030,12030,12230,12430,12630,12830,4395
3869  REM  CONTROL FUNCTIONS<CTRL-J>
3870  FOR X = 1 TO CM(0): IF IK$ < > MID$ (CM$(0),X,1)  THEN  NEXT : GOTO 3798
3875  ON X GOTO 5630,5420,4190,8755,4395,4465,7305,6700,4010,4710,4850,4970,4295,4850,4250,5200,8785,7485,5550,7405,4355,4110,5310,7495,7505,6090,7555,5630,4395
3880 X = 0: GOTO 3798: REM <CTRL-J>
3890  REM 
3891  REM  CHNG HELP SCREEN<CTRL-J>
3895 HS = HS +1: IF HS >4  THEN HS = 1
3896  ON HS GOSUB 15205,15305,15405,15463
3897  GOTO 3780: REM <CTRL-J>
4000  REM 
4001  REM  CUR LEFT<CTRL-J>
4010 PO = PO -1: IF PO <0  THEN PO = IL: GOSUB 4780: PRINT  CHR$(7);
4050  GOTO 3780: REM <CTRL-J>
4090  REM 
4091  REM  CUR RIGHT<CTRL-J>
4110 PO = PO +1: IF PO >IL  THEN PO = 0: GOSUB 4780: PRINT  CHR$(7)
4140  GOTO 3780: REM <CTRL-J>
4170  REM 
4171  REM  CUR TO BEG OF LINE<CTRL-J>
4190 PO = 0: GOTO 3780: REM  <CTRL-J>
4210  REM 
4211  REM  CUR TO EOL<CTRL-J>
4250 PO = IL: GOTO 3780: REM <CTRL-J>
4290  REM 
4291  REM  BKSP & ERASE<CTRL-J>
4295 PO = PO -1: IF PO <0  THEN PO = IL: GOSUB 4780
4300  VTAB (18): HTAB 5 +PO: PRINT " <CTRL-H>";
4305  PRINT  CHR$(7);
4310  IF F1  THEN 4395
4320  GOTO 3780: REM  <CTRL-J>
4350  REM 
4351  REM  TAB TO CENTR<CTRL-J>
4355  IF PO <(IL +1)/2  THEN PO = (IL +1)/2: GOTO 3780
4360  IF (PO > = (IL +1)/2)  AND PO <IL  THEN PO = IL: GOTO 3780
4365  IF PO = IL  THEN PO = 0: GOTO 3780
4368 PO = 0: GOTO 3780: REM  <CTRL-J>
4390  REM 
4391  REM  DEL CHAR<CTRL-J>
4395  FOR X = PO TO IL: POKE PZ +X, PEEK(PZ +X +1): NEXT 
4400  VTAB (18): HTAB 5 +IL: PRINT " ";: HTAB 6 +PO: PRINT "<CTRL-H>";
4410 F2 = 1: GOTO 3780: REM <CTRL-J>
4460  REM 
4461  REM  ERASE TO EOL<CTRL-J>
4465  PRINT  SPC( (IL +1) -PO);: HTAB (6 +PO): PRINT "<CTRL-H>";
4470 F2 = 1: GOTO 3780: REM <CTRL-J>
4550  REM 
4551  REM  INS CHAR<CTRL-J>
4555  FOR X = IL TO PO +0  STEP  -1: POKE PZ +X, PEEK((PZ +X) -1): NEXT 
4560  VTAB (18): HTAB 5 +PO: PRINT IK$;:PO = PO +1: IF PO >IL  THEN PO = 0: GOSUB 4780
4565 F2 = 1: GOTO 3780: REM  <CTRL-J>
4670  REM 
4671  REM  TOGGLE INS ON/OFF<CTRL-J>
4710  GOSUB 4720: GOTO 3780
4720  IF F1 = 0  THEN F1 = 1: GOTO 4790: REM  <CTRL-J>
4750  REM 
4751  REM  INS OFF<CTRL-J>
4780 F1 = 0
4790  VTAB (20): HTAB 31: PRINT MG$(F1);
4800  RETURN : REM  <CTRL-J>
4830  REM 
4831  REM  CUR DN<CTRL-J>
4850  IF EN = 1  THEN 3780
4860  IF F2 >0  THEN  GOSUB 5090
4870  IF F1 >0  THEN  GOSUB 4780
4890 LN = LN +1: IF LN >EN  THEN LN = 1
4900  GOSUB 5710: GOTO 3780: REM  <CTRL-J>
4950  REM 
4951  REM  CUR UP<CTRL-J>
4970  IF EN = 1  THEN 3780
4980  IF F2 >0  THEN  GOSUB 5090
4990  IF F1 >0  THEN  GOSUB 4780
5010 LN = LN -1: IF LN <1  THEN LN = EN
5020  GOSUB 5710: GOTO 3780: REM  <CTRL-J>
5070  REM 
5071  REM  CURNT LINE INTO ARRAY<CTRL-J>
5090 PO = 0:DA$(LN) = "": FOR X = PZ TO PZ +23
5100 Z =  PEEK(X) -128
5120 DA$(LN) = DA$(LN) + CHR$(Z)
5130  NEXT X: IF F2 >0  THEN F3 = 1
5150  GOSUB 4780: RETURN : REM  <CTRL-J>
5180  REM 
5181  REM  PG UP<CTRL-J>
5200  IF F2 >0  THEN  GOSUB 5090
5210  IF F1 >0  THEN  GOSUB 4780
5220  IF EN <12  THEN 4850
5240 LN = LN +11: IF LN >EN  THEN LN = LN -EN
5250  GOSUB 5710: GOTO 3780: REM  <CTRL-J>
5290  REM 
5291  REM  PG DN<CTRL-J>
5310  IF F2 >0  THEN  GOSUB 5090
5320  IF F1 >0  THEN  GOSUB 4780
5330  IF EN <12  THEN 4970
5350 LN = LN -11: IF LN <1  THEN LN = LN +EN
5360  GOSUB 5710: GOTO 3780: REM  <CTRL-J>
5400  REM 
5401  REM  JMP NXT LBL<CTRL-J>
5420  IF EN = 1  THEN 3750
5430  IF F2 >0  THEN  GOSUB 5090
5440  IF F1 >0  THEN  GOSUB 4780
5450  GOSUB 13760: IF LN >EN  THEN LN = 1
5460  GOSUB 5710: GOTO 3780: REM  <CTRL-J>
5530  REM 
5531  REM  RESTOR LINE<CTRL-J>
5550  VTAB (18): HTAB 5: PRINT DA$(LN);
5570 F2 = 0: GOTO 3780: REM <CTRL-J>
5610  REM 
5611  REM  EXIT TO MAIN MENU<CTRL-J>
5630  IF F2 >0  THEN  GOSUB 5090
5640  IF F1 >0  THEN  GOSUB 4780
5650  GOSUB 13630: GOTO 2010: REM  <CTRL-J>
5690  REM 
5691  REM  PRINT TEXT<CTRL-J>
5710  VTAB (13): IF EN >11  THEN 5920
5730 CL = LN -5: IF CL <0  THEN CL = LN -6
5750  FOR X = 1 TO 5
5760  IF CL <1  THEN  HTAB 1: PRINT BLANK$: GOTO 5790
5770  HTAB 1: PRINT LN$(CL)" |"DA$(CL)
5790 CL = CL +1: IF CL = 0  THEN CL = 1
5800  NEXT X
5820 CL = LN: VTAB (18)
5830  FOR X = 6 TO 11
5850  IF CL >EN  THEN  HTAB 1: PRINT BLANK$: GOTO 5880
5860  HTAB 1: PRINT LN$(CL)" |"DA$(CL)
5880 CL = CL +1: NEXT X
5900  GOTO 6020: REM  <CTRL-J>
5920 CL = LN -5
5940  IF CL < = 0  THEN CL = EN +CL
5950  IF CL = 0  THEN CL = 1
5960  VTAB (13)
5970  FOR X = 1 TO 11
5980  HTAB 1: PRINT LN$(CL)" |"DA$(CL)
6000 CL = CL +1: IF CL >EN  THEN CL = 1
6010  NEXT X
6020  GOSUB 11330
6030 IL = 23: IF DA(LN) <5  AND DT(LN) >0  THEN IL = 11
6040  RETURN : REM  <CTRL-J>
6070  REM 
6071  REM  DEL LBL/DATA<CTRL-J>
6090  IF EN = 1  THEN EN =  -1: GOTO 6280
6100 MG = 12: IF DA(LN) <5  THEN OS = 1: GOTO 6200
6110 LA(DT(LN)) = LA(DT(LN)) -1
6120  FOR X = LN +1 TO EN: IF DA(X) <5  THEN  NEXT 
6130  IF X -LN = 1  OR LN = EN  THEN OS = 1: GOTO 6200
6140  GOSUB 13430: VTAB (9): HTAB 39: PRINT "<CTRL-H>";
6150  GET IK$: IF IK$ = "N"  OR IK$ = "n"  THEN 3750
6160  IF IK$ = "Y"  OR IK$ = "y"  THEN  GOSUB 13630: GOTO 6180
6165  GOTO 6140
6180 OS = X -LN: IF X >EN  THEN LN = LN -1:EN = LN: GOTO 6280
6200 EN = EN -OS:MG = 13: GOSUB 13430
6220  FOR X = LN TO EN:DA$(X) = DA$(X +OS)
6240 DA(X) = DA(X +OS):DT(X) = DT(X +OS): NEXT 
6280  IF EN <1  THEN EN = 1:LN = 1:DA$(1) = DL$(0):LA(0) = 1:DT(1) = 0
6290  IF LN >EN  THEN LN = EN
6300  IF LA(DT(LN)) <0  THEN LA(DT(LN)) = 0
6390 F3 = 1: GOSUB 5710: GOTO 3750: REM <CTRL-J>
6670  REM 
6671  REM  INS LBL/DATA<CTRL-J>
6700  IF EN = EF  THEN MG = 7: GOSUB 13230: GOTO 3700
6710 MU = 2: GOSUB 3340: IF F2 = 1  THEN  GOSUB 5090
6720 RQ = 4:MG = 11: GOSUB 13430: GOSUB 3540: IF IK$ =  CHR$(27)  THEN 3700
6730 IK =  VAL(IK$) -2: IF IK <0  THEN IK = 0:RQ = 5
6740 Q1 = 0:PO = 0: IF LA(IK) = 0  THEN RQ = 5
6770  IF RQ = 5  THEN  FOR X = 0 TO 3:Q1 = Q1 +LA(X): NEXT : IF Q1 = 12  THEN MG = 8: GOSUB 13230: GOTO 6700
6780 X = LN: IF DT(LN) = IK  THEN 6810
6790  IF LA(IK)  THEN  FOR X = LN TO EN: IF DT(X) < >IK  THEN  NEXT : FOR X = 1 TO LN: IF DT(X) < >IK  THEN  NEXT 
6800  IF X = LN  THEN X = EN: IF IK  THEN X = 0
6810 LN = X +1:EN = EN +1: FOR X = EN TO LN +1  STEP  -1:DA$(X) = DA$(X -1)
6820 DA(X) = DA(X -1):DT(X) = DT(X -1): NEXT 
6830 DA$(LN) = DD$(IK): IF RQ = 5  THEN DA$(LN) = DL$(IK):LA(IK) = LA(IK) +1
6840 DA(LN) = RQ:DT(LN) = IK:F3 = 1: GOSUB 5710: GOTO 6700: REM <CTRL-J>
7300  REM 
7301  REM  FIND CITY<CTRL-J>
7305  IF DA(LN) = 5  OR DT(LN) <3  THEN 7395
7310 IP$ = "": VTAB (9): HTAB 3: PRINT MG$(28);: GOSUB 13930: GOSUB 14510
7320  IF IP$ = ""  THEN 7395
7325 IP$ =  LEFT$(IP$,12)
7330 WT = 1:MG = 31: GOSUB 13430: REM  <CTRL-J>
7340  PRINT D$;"OPEN TIMEZONE.DAT, L19"
7341  PRINT D$;"READ TIMEZONE.DAT,R";WT
7342  VTAB (11): HTAB 1: INPUT WT$
7343  IF IP$ =  LEFT$(WT$, LEN(IP$))  THEN  GOSUB 13630: GOTO 7580
7344 WT = WT +1: IF WT <WF +1  THEN 7341: REM  <CTRL-J>
7350  PRINT D$;"CLOSE TIMEZONE.DAT"
7355  VTAB (11): HTAB 1: PRINT MG$(5)FI$;: IF FI$ = ""  THEN  PRINT MG$(34);
7395  GOSUB 13630: GOTO 3870: REM  <CTRL-J>
7400  REM 
7401  REM  SEARCH FILE<CTRL-J>
7405  IF LN = EN  THEN 7460
7415 IP$ = "": VTAB (9): HTAB 2: PRINT MG$(29);: GOSUB 13930
7420  GOSUB 14510: IF IP$ = ""  THEN 7455
7430 MG = 31: GOSUB 13430
7435 X = LN +1
7440  IF IP$ =  LEFT$(DA$(X), LEN(IP$))  THEN LN = X: GOTO 7455
7445 X = X +1: IF X <EN +1  THEN 7440
7450 MG = 30: GOSUB 13230
7455  GOSUB 13630
7456  IF X = LN  THEN  GOSUB 5710
7460  GOTO 3750: REM  <CTRL-J>
7480  REM 
7481  REM  BEG OF FILE<CTRL-J>
7485  GOSUB 5090:PO = 0
7486 LN = 1: GOSUB 5710: GOTO 3750: REM  <CTRL-J>
7490  REM 
7491  REM  END OF FILE<CTRL-J>
7495  GOSUB 5090:PO = 0
7496 LN = EN: GOSUB 5710: GOTO 3750: REM  <CTRL-J>
7500  REM 
7501  REM  ROLL CITIES DN<CTRL-J>
7505  IF DA(LN) = 5  OR DT(LN) <3  THEN 7595
7506  PRINT D$;"OPEN TIMEZONE.DAT, L19"
7510 WT = WT -1: IF WT <1  THEN WT = WF
7513  PRINT D$;"READ TIMEZONE.DAT,R";WT
7514  VTAB (11): HTAB 1: INPUT WT$
7515  VTAB (11): HTAB 1: PRINT MG$(5)FI$;: IF FI$ = ""  THEN  PRINT MG$(34);
7520  GOTO 7580: REM  <CTRL-J>
7550  REM 
7551  REM  ROLL CITIES UP<CTRL-J>
7555  IF DA(LN) = 5  OR DT(LN) <3  THEN 7595
7556  PRINT D$;"OPEN TIMEZONE.DAT, L19"
7560 WT = WT +1: IF WT >WF  THEN WT = 1
7563  PRINT D$;"READ TIMEZONE.DAT,R";WT
7564  VTAB (11): HTAB 1: INPUT WT$
7565  VTAB (11): HTAB 1: PRINT MG$(5)FI$;: IF FI$ = ""  THEN  PRINT MG$(34);
7580  PRINT D$;"CLOSE TIMEZONE.DAT"
7581  VTAB (18): HTAB 5
7585  PRINT  LEFT$(WT$,15)":" RIGHT$(WT$,2)" = TZ ";
7590 F2 = 1
7595  GOTO 3780: REM  <CTRL-J>
7600  REM 
7601  REM  LOAD WATCH<CTRL-J>
7610 Q1 = LN:MG = 10: GOSUB 13430
7620 LO = 1094: IF SLOT >0  THEN  PR# SLOT
7630 DA$(0) =  CHR$(0) +"L"
7635  FOR X = 1 TO EN
7640  IF DA(X) = 5  THEN  GOSUB 8705
7645  NEXT 
7650 DA$(O) =  LEFT$(DA$(0) +"@" + CHR$(0) +SP$,26): REM <CTRL-J>
7665 PR$ = DA$(0): GOSUB 8735: REM <CTRL-J>
7685 X = 1
7690 X$ = " " + STR$(X):X$ =  RIGHT$(X$,2): POKE LO, ASC(X$) +128: POKE LO +1, ASC( RIGHT$(X$,1)) +128
7692  ON DT(X) +1 GOTO 7740,7805,8005,8205
7695 X = X +1: IF X < = EN  THEN 7690: REM  <CTRL-J>
7697  FOR X = EN +1 TO 80
7698 X$ = " " + STR$(X):X$ =  RIGHT$(X$,2): POKE LO, ASC(X$) +128: POKE LO +1, ASC( RIGHT$(X$,1)) +128
7699 PR$ = "L" +DA$(X): GOSUB 8735
7700  NEXT :PR$ = "L": GOSUB 8735:LN = Q1: PR# 0: RETURN : REM  <CTRL-J>
7740 PR$ =  CHR$(100): IF DA(X) = 5  THEN PR$ = "L"
7745 PR$ = PR$ +DA$(X)
7750  GOSUB 8735: GOTO 7695: REM <CTRL-J>
7805  IF DA(X) = 5  THEN 7740
7810 AP = 0: GOSUB 8505: REM  <CTRL-J>
7825  FOR J = 0 TO LL
7826  POKE LO +4,173
7827 AP = 0
7835 MO$ =  MID$ (DA$(BL +J),13,2)
7840 DY$ =  MID$ (DA$(BL +J),16,2)
7845 HR$ =  MID$ (DA$(BL +J),20,2)
7847 AP$ =  MID$ (DA$(BL +J),19,1): IF AP$ = "P"  AND  VAL(HR$) <12  THEN AP = 12
7848  IF AP$ = "A"  AND  VAL(HR$) = 12  THEN HR$ = "00"
7850 MI$ =  MID$ (DA$(BL +J),23)
7855 HR$ =  STR$( VAL(HR$) +AP)
7856 HR$ = "0" +HR$
7857 HR$ =  RIGHT$(HR$,2)
7865 SD(J) =  VAL(MO$ +DY$ +HR$ +MI$)
7870 SN(J) =  VAL(LN$(BL +J))
7871  POKE LO +4,160
7875  NEXT J: REM  <CTRL-J>
7885  GOSUB 8605
7895  FOR J = 0 TO LL -1
7900 PR$ =  CHR$(100) +DA$(SN(J))
7905  GOSUB 8735
7910  NEXT J: REM  <CTRL-J>
7915 X = EL
7920  GOTO 7695
8005  IF DA(X) = 5  THEN 7740
8010  GOSUB 8505: REM  <CTRL-J>
8020  FOR J = 0 TO LL -1
8022 AP = 0
8023  POKE LO +4,170
8025 AP$ =  MID$ (DA$(BL +J),19,1)
8030 DY$ =  MID$ (DA$(BL +J),13,1)
8035 HR$ =  MID$ (DA$(BL +J),20,2)
8040 MI$ =  MID$ (DA$(BL +J),23,2)
8042  IF AP$ = "P"  AND  VAL(HR$) <12  THEN AP = 12
8043  IF AP$ = "A"  AND  VAL(HR$) = 12  THEN HR$ = "00"
8045 HR$ =  STR$( VAL(HR$) +AP)
8046 HR$ = "0" +HR$
8047 HR$ =  RIGHT$(HR$,2)
8055 SD(J) =  VAL(DY$ +HR$ +MI$)
8060 SN(J) =  VAL(LN$(BL +J))
8061  POKE LO +4,160
8065  NEXT J: REM  <CTRL-J>
8075  GOSUB 8605
8085  FOR J = 0 TO LL -1
8090 PR$ =  CHR$(100) +DA$(SN(J))
8095  GOSUB 8735
8100  NEXT J: REM  <CTRL-J>
8105 X = EL
8110 X = EL: GOTO 7695
8205  IF DA(X) = 5  THEN 7740
8215 HR$ =  MID$ (DA$(X),14,2)
8220 MI$ =  MID$ (DA$(X),17,2)
8225 HR =  VAL(HR$)
8228 AP$ = "0"
8229 HR = HR -TZ: IF HR <0  THEN HR = HR +24
8230  IF HR >11  THEN AP$ = "1":HR = HR -12
8231 HR$ =  STR$(HR)
8232 HR$ = "0" +HR$
8233 HR$ =  RIGHT$(HR$,2)
8240 PR$ =  CHR$(100) + LEFT$(DA$(X),12)
8245 PR$ = PR$ +AP$ +HR$ +MI$ +"       "
8250  GOSUB 8735: GOTO 7695: REM <CTRL-J>
8500  REM - - - -
8501  REM  <CTRL-J>
8505 BL = X:LN = X
8510  GOSUB 13760
8515  IF LN >BL  THEN EL = LN -1
8520  IF LN = 1  THEN EL = EN
8525 LL = (EL -BL) +1
8530  RETURN : REM <CTRL-J>
8600  REM - - - -
8601  REM  SORT<CTRL-J>
8605 G% = LL:N% = LL
8615  IF G% >1  THEN G% = G%/2: GOTO 8620
8617  GOTO 8665: REM  -->OUTSIDE 'WHILE' DONE
8620  FOR FZ = 1 TO 1
8625  FOR CZ = 0 TO N% -G% -1
8630  IF SD(CZ) >SD(G% +CZ)  THEN 8634
8632  GOTO 8650: REM  -->INSIDE 'WHILE' DONE
8634  POKE LO +4,170
8635 Z = SD(CZ):SD(CZ) = SD(CZ +G%):SD(G% +CZ) = Z
8640 Z = SN(CZ):SN(CZ) = SN(CZ +G%):SN(G% +CZ) = Z
8645 FZ = 0: POKE LO +4,160
8649  GOTO 8630
8650  REM -->END OF INSIDE 'WHILE'
8655  NEXT CZ
8660  NEXT FZ
8664  GOTO 8615
8665  REM -->END OF OUTSIDE 'WHILE'
8666  POKE LO +4,160
8670  RETURN : REM  <CTRL-J>
8700  REM 
8701  REM  CALC LBL ADDR<CTRL-J>
8705 Y = (X -1) *25
8710 Y = Y +(4096 *DT(X))
8715 DA$(0) = DA$(0) + CHR$( INT(Y/256)) + CHR$((Y/256 - INT(Y/256)) *256)
8720  RETURN : REM  <CTRL-J>
8730  REM 
8731  REM WATCH OUTPUT-SLOT<CTRL-J>
8735  IF SLOT = 0  THEN  GOTO 8742
8736  FOR I = 1 TO  LEN(PR$)
8737  POKE 781,( ASC( MID$ (PR$,I,1))): CALL 780
8738  NEXT I: RETURN : REM <CTRL-J>
8740  REM 
8741  REM WATCH OUTPUT-GAME PORT<CTRL-J>
8742  FOR I = 1 TO  LEN(PR$)
8743 U =  USR( ASC( MID$ (PR$,I,1)))
8745  NEXT I: RETURN : REM <CTRL-J>
8750  REM 
8751  REM  LOAD C&P BUFFER<CTRL-J>
8755  GOSUB 5090:CT = DT(LN):CT$ = DA$(LN):HS = 3: GOTO 3700: REM  <CTRL-J>
8780  REM 
8781  REM  PASTE<CTRL-J>
8785  IF CT$ = ""  THEN 3780
8790  VTAB (18): HTAB 5:PO = 0
8795  IF DT(LN) = (CT)  OR (DT(LN)) = 0  OR DA(LN) = 5  THEN  GOTO 8805
8800  PRINT  LEFT$(CT$,12);: GOTO 8810
8805  PRINT CT$;
8810  VTAB (18): HTAB 6 +PO: PRINT "<CTRL-H>";:F2 = 1: GOTO 3780: REM  <CTRL-J>
9000  REM ============
9001  REM  DISK I/0
9002  REM ============<CTRL-J>
9003  REM 
9004  REM  READ DISK FILE<CTRL-J>
9070  IF FI$ = ""  THEN MG = 27: GOSUB 13230: GOTO 9220
9071  IF F3 >0  THEN MG = 21: GOSUB 13430:VT =  PEEK(37) +1:HT =  PEEK(36)
9072  IF F3 <1  THEN  GOTO 9079
9073  GET IK$: IF IK$ = ""  THEN 9073
9074  IF IK$ = "Y"  THEN 9079
9075  IF IK$ = "y"  THEN 9079
9076  IF IK$ = "N"  THEN 9220
9077  IF IK$ = "n"  THEN 9220
9078  VTAB (VT): HTAB HT: PRINT  CHR$(7);: GOTO 9073: REM <CTRL-J>
9079  ONERR  GOTO 9550
9080 MG = 16: GOSUB 13430
9090  PRINT D$;"OPEN "FI$",L27"
9140 LA(0) = 0:LA(1) = 0:LA(2) = 0:LA(3) = 0
9150  FOR X = 0 TO EF +1:DA$(X) = ""
9152  PRINT D$;"READ "FI$",R"X +1
9160  FOR Y = 1 TO 27: GET PR$:DA$(X) = DA$(X) +PR$: NEXT Y
9170  IF X >0  AND X <81  THEN  GOSUB 9930
9180  NEXT X
9185 EN =  VAL(DA$(0)):TZ =  VAL(DA$(81))
9190  POKE 216,0
9200  PRINT D$;"CLOSE "FI$
9210 F3 = 0:LN = 1: GOSUB 5710
9220  GOSUB 11330: RETURN : REM <CTRL-J>
9300  REM 
9301  REM  WRITE DISK FILE<CTRL-J>
9330  IF FI$ = ""  THEN MG = 27: GOSUB 13230: GOTO 9520
9335  IF F2 = 1  THEN  GOSUB 5090
9340  ONERR  GOTO 9590
9350  PRINT D$;"OPEN "FI$",L27"
9351  PRINT D$;"READ "FI$",R1"
9352  ONERR  GOTO 9590
9353  VTAB 8: HTAB 2: PRINT " <CTRL-H> ";
9354  INPUT IP$
9355  PRINT D$;"CLOSE "FI$
9360 MG = 26: GOSUB 13430
9370  GET IK$: IF IK$ = ""  THEN 9370
9371  IF IK$ = "Y"  THEN 9410
9372  IF IK$ = "y"  THEN 9410
9373  IF IK$ = "N"  THEN 9490
9374  IF IK$ = "n"  THEN 9490
9375  IF IK$ =  CHR$(27)  THEN  STOP 
9390  PRINT  CHR$(7);: GOTO 9370: REM <CTRL-J>
9410 MG = 15: GOSUB 13430
9420  ONERR  GOTO 9550
9430  PRINT D$;"OPEN "FI$",L27"
9431  PRINT D$;"WRITE "FI$",R1"
9440  PRINT  STR$(EN) + RIGHT$(PAD$,26 - LEN( STR$(EN))) + CHR$(13)
9450  FOR X = 1 TO EF
9460  GOSUB 10030
9470  PRINT D$;"WRITE "FI$",R";X +1
9471  PRINT PR$ + CHR$(13)
9480  NEXT X
9485  PRINT D$;"WRITE "FI$",R";X +1
9486  PRINT  STR$(TZ) + RIGHT$(PAD$,26 - LEN( STR$(TZ))) + CHR$(13)
9487 F3 = 0
9490  PRINT D$;"NOMON C,I,O"
9492  PRINT D$;"CLOSE "FI$
9500  POKE 216,0
9510  PRINT D$;"CLOSE "FI$
9520  GOSUB 13630
9525  GOSUB 11330
9530  RETURN : REM  <CTRL-J>
9540  REM 
9541  REM  I/O ERROR TRAP<CTRL-J>
9550  CALL 768: GOTO 9560
9560  PRINT D$;"CLOSE "FI$
9565  POKE 216,0:MG = 17: GOSUB 13230
9570  GOTO 9500: REM  <CTRL-J>
9580  REM 
9581  REM  NO FILE 'ERROR TRAP'<CTRL-J>
9590  CALL 768: GOTO 9592
9592  PRINT D$;"CLOSE "FI$
9593  IF  PEEK(222) = 6  THEN  PRINT D$;"DELETE"FI$
9595  IF  PEEK(222)  THEN  POKE 216,0: GOTO 9410
9596  GOTO 9565: REM <CTRL-J>
9900  REM 
9901  REM  UNPACK FILE<CTRL-J>
9930 DA(X) = 4:DT(X) =  VAL( RIGHT$(DA$(X),2))
9940  IF  LEFT$(DA$(X),1) = "L"  THEN DA(X) = 5:LA(DT(X)) = LA(DT(X)) +1
9950 LN$(X) =  RIGHT$(" " + STR$(X),2)
9960 DA$(X) =  MID$ (DA$(X),2,24)
9990  RETURN : REM  <CTRL-J>
10000  REM 
10001  REM   PACK DATA FILE - SBR<CTRL-J>
10030 PR$ =  CHR$(100): IF DA(X) = 5  THEN PR$ = "L"
10060 PR$ = PR$ +DA$(X) + RIGHT$( STR$(DT(X)),2)
10080  RETURN : REM <CTRL-J>
10100  REM 
10101  REM  CREATE "EMPTY" FILE<CTRL-J>
10130 DA(1) = 5:DT(1) = 0:LN$(1) = " 1":DA$(1) = DL$(0)
10140  FOR X = 2 TO EF
10150 DA(X) = 4:DT(X) = 0
10160 LN$(X) =  RIGHT$(" " + STR$(X),2)
10170 DA$(X) = DD$(0)
10180  NEXT X
10190 EN = 1:LN = 1:LA(0) = 1:FI$ = ""
10200  RETURN : REM <CTRL-J>
10300  REM  
10301  REM   DATE & TIME<CTRL-J>
10330  GOSUB 13630:MG = 18: VTAB (8): HTAB 3: PRINT MG$(MG)
10350 IP$ = "": GOSUB 13930: GOSUB 14510
10360  IF IP$ = ""  THEN 10400
10365  IF  LEN(IP$) <8  THEN : GOSUB 10500: GOTO 10330
10370 IP$ =  LEFT$(IP$,8)
10375  IF  VAL( LEFT$(IP$,2)) >24  OR  VAL( MID$ (IP$,4,2)) >59  OR  VAL( MID$ (IP$,7)) >59  THEN  GOSUB 10500: GOTO 10330
10376  IF  VAL( LEFT$(IP$,2)) <1  OR  VAL( MID$ (IP$,4,2)) <0  OR  VAL( MID$ (IP$,7)) <0  THEN  GOSUB 10500: GOTO 10330
10380 TIME$ = IP$: REM <CTRL-J>
10400  GOSUB 13630:MG = 19: VTAB (8): HTAB 3: PRINT MG$(MG)
10410 IP$ = "": GOSUB 13930: GOSUB 14510
10415  IF IP$ = ""  THEN 10438
10416  IF  LEN(IP$) <8  THEN : GOSUB 10500: GOTO 10330
10418 IP$ =  LEFT$(IP$,8)
10420 MO =  VAL( LEFT$(IP$,2)):DY =  VAL( MID$ (IP$,4,2)):YR =  VAL( RIGHT$(IP$,2))
10430  IF MO <1  OR MO >12  OR DY <1  OR DY >31  OR YR <80  THEN  GOSUB 10500: GOTO 10400
10431  IF MO = 2  AND YR/4 - INT(YR/4) = 0  AND DY <30  THEN  GOTO 10435
10432  IF MO = 2  AND DY >28  THEN  GOSUB 10500: GOTO 10400
10435 YEAR$ = IP$
10438  VTAB (24): HTAB 13: PRINT YEAR$;: IF INIT = 0  THEN INIT = 1: GOTO 1250
10440  GOTO 2950: REM <CTRL-J>
10500 MG = 20: GOSUB 13230: RETURN : REM  <CTRL-J>
11000  REM ====================
11001  REM   UTILITIES
11002  REM ====================<CTRL-J>
11310  REM 
11311  REM  PRNT WATCH MEM STAT<CTRL-J>
11330  VTAB (11): HTAB 1: PRINT  SPC( 40)
11331  VTAB (11): HTAB 1: PRINT MG$(5)FI$
11332  IF FI$ = ""  THEN  VTAB (11): HTAB 13: PRINT MG$(34)
11335  VTAB (13): HTAB 31: PRINT MG$(3)" "80 -EN
11336  IF 80 -EN <10  THEN  VTAB (14): HTAB 40: PRINT " ";
11350  VTAB (15): HTAB 31: PRINT MG$(4)" "12 -(LA(0) +LA(1) +LA(2) +LA(3))
11351  IF 12 -(LA(0) +LA(1) +LA(2) +LA(3)) <10  THEN  VTAB (16): HTAB 40: PRINT " ";
11360  VTAB (23): HTAB 31: PRINT MG$(33)" "TZ" ";
11370  VTAB (18): HTAB 31: PRINT DT$(DA(LN))DT$(DT(LN))
11420  RETURN : REM <CTRL-J>
11500  REM 
11501  REM  INP FILE NAME<CTRL-J>
11530  GOSUB 13630: VTAB (8): HTAB (2): PRINT MG$(25);
11550  GOSUB 13930
11560 IP$ = "": GOSUB 14510
11570  IF IP$ = ""  THEN 11630
11571  IF IP$ =  CHR$(27)  THEN 11630
11580  IF  ASC(IP$) <65  THEN MG = 20: GOSUB 13230: GOTO 11530
11590 FI$ = IP$
11600  GOSUB 11330
11610  GOSUB 13630
11630  GOTO 2950: REM <CTRL-J>
12000  REM 
12001  REM  ROLL AM/PM<CTRL-J>
12030  IF IL = 23  OR DT(LN) = 3  THEN 3780
12040 F2 = 1
12050  IF  PEEK(1254) = 193  THEN  POKE 1254,208: GOTO 3780
12070  POKE 1254,193: GOTO 3780: REM  <CTRL-J>
12200  REM 
12201  REM   ROLL HOURS<CTRL-J>
12230  IF IL = 23  THEN 3780
12240 LO = 1249:HT = 18: IF DT(LN) <3  THEN LO = 1255:HT = 24
12280  GOSUB 13120
12282 PR = PR +1
12285  IF DT(LN) = 3  AND PR >24  THEN PR = 1
12286  IF DT(LN) = 3  THEN 12300
12290  IF PR >12  THEN PR = 1
12300  GOSUB 13160
12310 F2 = 1: GOTO 3780: REM <CTRL-J>
12400  REM 
12401  REM  ROLL MINS<CTRL-J>
12430  IF IL = 23  THEN 3780
12440 LO = 1252:HT = 21: IF DT(LN) <3  THEN LO = 1258:HT = 27
12450  GOSUB 13120
12490 PR = PR +1: IF PR >59  THEN PR = 0
12500  GOSUB 13160
12510 F2 = 1: GOTO 3780: REM <CTRL-J>
12600  REM 
12601  REM  ROLL MNTHS<CTRL-J>
12630  IF IL = 23  OR DT(LN) >1  THEN 3780
12640 LO = 1248:HT = 17
12660  GOSUB 13120
12670 PR = PR +1: IF PR >12  THEN PR = 1
12680  GOSUB 13160
12690  IF DT(LN) = 1  THEN  VTAB (18): HTAB 20: PRINT "01";
12700 F2 = 1: GOTO 3780: REM <CTRL-J>
12800  REM 
12801  REM  ROLL DAYS<CTRL-J>
12830  IF IL = 23  OR DT(LN) = 3  THEN 3780
12840  IF DT(LN) = 2  THEN LO = 1248:HT = 17: GOTO 12970
12845 LO = 1248
12850  GOSUB 13120
12860 DY$ =  MID$ ("312831303130313130313031", VAL(PR$) *2 -1,2)
12890  IF MO > VAL(PR$)  THEN YR = YR +1
12900  IF YR/4 - INT(YR/4) = 0  AND PR$ = "02"  THEN DY$ = "29"
12905  IF MO > VAL(PR$)  THEN YR = YR -1
12910 LO = 1251: GOSUB 13120
12920 PR = PR +1: IF PR > VAL(DY$)  THEN PR = 1
12940 HT = 20: GOSUB 13160
12950 F2 = 1: GOTO 3780
12960  REM 
12970  GOSUB 13120
12980 PR = PR +1: IF PR >6  THEN PR = 0
12990  VTAB (18): HTAB HT: PRINT DY$(PR);
13010 F2 = 1: GOTO 3780: REM <CTRL-J>
13100  REM 
13101  REM  GET TGT STRING<CTRL-J>
13120 PR$ =  CHR$( PEEK(LO) -128) + CHR$( PEEK(LO +1) -128)
13130 PR =  VAL(PR$): RETURN : REM <CTRL-J>
13150  REM 
13151  REM  PRINT VAL IN TARG AREA<CTRL-J>
13160  VTAB (18): HTAB HT
13165  IF PR >9  THEN  PRINT  RIGHT$( STR$(PR),2);
13170  IF PR <10  THEN  PRINT "0" + RIGHT$( STR$(PR),1);
13180  RETURN : REM <CTRL-J>
13200  REM 
13201  REM  PRNT ER MSG<CTRL-J>
13230 Z = (39 - LEN(MG$(MG)))/2: IF Z <2  THEN Z = 2
13232  GOSUB 13630
13250  VTAB (8): HTAB 2: PRINT  SPC( Z)MG$(MG)
13280  PRINT  CHR$(7): FOR Y = 1 TO 1000: NEXT Y
13282  GOSUB 13630
13320  RETURN : REM <CTRL-J>
13400  REM 
13401  REM  PRNT MSG<CTRL-J>
13430  GOSUB 13630
13440 Z = (39 - LEN(MG$(MG)))/2: IF Z <2  THEN Z = 2
13450  VTAB (9): HTAB Z: PRINT MG$(MG);
13470  FOR Y = 1 TO 70: NEXT Y
13490  RETURN : REM <CTRL-J>
13600  REM 
13601  REM  CLR MSG<CTRL-J>
13630  VTAB (8): HTAB 2: PRINT SP$"  ";
13660  VTAB (9): HTAB 2: PRINT SP$"  ";
13670  RETURN : REM <CTRL-J>
13700  REM 
13701  REM  FIND LBL<CTRL-J>
13760  FOR X = LN +1 TO EN
13770  IF DA(X) = 5  THEN LN = X: RETURN 
13780  NEXT :LN = 1: RETURN : REM <CTRL-J>
13900  REM 
13901  REM  PROMPT SOUND<CTRL-J>
13930  FOR X = 1 TO 3: PRINT  CHR$(7);: NEXT X
13960  RETURN : REM <CTRL-J>
14500  REM 
14501  REM  GEN INPUT<CTRL-J>
14510 X = 0
14520  VTAB (9): HTAB 14: PRINT " "IP$" <CTRL-H>";
14530  GET IK$
14532  IF IK$ =  CHR$(13)  THEN 14640
14534  IF IK$ =  CHR$(8)  OR IK$ =  CHR$(127)  THEN 14615
14536  IF IK$ =  CHR$(27)  THEN 14660
14560  IF  ASC(IK$) >31  AND  ASC(IK$) <127  THEN 14585
14568  PRINT  CHR$(7);: GOTO 14530: REM <CTRL-J>
14585 X = X +1: IF X >24  THEN X = 24: PRINT  CHR$(7);: GOTO 14530
14590 IP$ = IP$ +IK$
14595  GOTO 14520
14615  PRINT  CHR$(7):X = X -1: IF X <1  THEN X = 0:IP$ = "": GOTO 14520
14620 IP$ =  LEFT$(IP$,X): GOTO 14520
14640  RETURN 
14660 IP$ = "": GOTO 14640: REM <CTRL-J>
15000  REM 
15001  REM   DRAW SCN<CTRL-J>
15010  HOME 
15020  PRINT " ________ PRESS <ESC> TO EXIT _________ ";
15025  FOR X = 1 TO 8: PRINT "|"; SPC( 38);"|";
15030  NEXT X
15040  PRINT "|______________________________________|";
15045  PRINT ""
15048  VTAB (12)
15050  PRINT "    ________________________ "
15051  VTAB (13)
15052  FOR X = 1 TO 5: PRINT "   |" SPC( 24)"|": NEXT X
15062  PRINT "   |" SPC( 24)"|<"
15064  FOR X = 1 TO 5: PRINT "   |" SPC( 24)"|": NEXT X
15074  PRINT "   |________________________|";
15076  VTAB (24): HTAB 13: PRINT YEAR$;
15080  VTAB (1)
15090  RETURN : REM <CTRL-J>
15200  REM 
15201  REM  HELP 1<CTRL-J>
15205  VTAB (2)
15210  HTAB 3: PRINT " ^ = CONTROL              ; = HELP  "
15212  INVERSE 
15215  HTAB 3: PRINT " ^H CURSOR LEFT    ^U CURSOR RIGHT  "
15220  HTAB 3: PRINT " ^B BEGIN LINE     ^N END LINE      "
15225  HTAB 3: PRINT " ^T TAB CENTER     ^E ERASE TO EOL  "
15230  HTAB 3: PRINT " ^D DEL CHAR       ^I INS CHAR      "
15235  HTAB 3: PRINT " ^L BKSP & ERASE <DEL>DEL CHAR      "
15240  GOTO 15480: REM  <CTRL-J>
15300  REM 
15301  REM  HELP 2<CTRL-J>
15305  VTAB (2)
15310  HTAB 3: PRINT " ^ = CONTROL  CR = RETURN  ; = HELP "
15312  INVERSE 
15315  HTAB 3: PRINT " ^K CURSOR UP      ^J CURSOR DOWN   "
15320  HTAB 3: PRINT " CR CURSOR DOWN    ^M CURSOR DOWN   "
15325  HTAB 3: PRINT " ^O PAGE UP        ^V PAGE DOWN     "
15330  HTAB 3: PRINT " ^Q CUR BEG FILE   ^W CUR END FILE  "
15335  HTAB 3: PRINT " ^S SEARCH FILE    ^A JUMP NXT LABEL"
15340  GOTO 15480: REM  <CTRL-J>
15400  REM 
15401  REM  HELP 3<CTRL-J>
15405  VTAB (2)
15410  HTAB 3: PRINT " ^ = CONTROL              ; = HELP  "
15412  INVERSE 
15415  HTAB 3: PRINT " ^Y DELETE LINE    ^G INSERT LINES  "
15420  HTAB 3: PRINT " ^C CUT LINE       ^P PASTE LINE    "
15421  IF CT$ < >""  THEN : VTAB (4): HTAB 17: PRINT "*";: VTAB (5)
15425  HTAB 3: PRINT " ^R RESTORE LINE                    "
15430  HTAB 3: PRINT "                                    "
15435  HTAB 3: PRINT "                                    "
15440  GOTO 15480: REM <CTRL-J>
15460  REM 
15461  REM  HELP 4<CTRL-J>
15463  VTAB (2)
15466  HTAB 3: PRINT " ^ = CONTROL              ; = HELP  "
15467  INVERSE 
15470  HTAB 3: PRINT "  ! SET AM/PM       @ SET AM/PM     "
15472  HTAB 3: PRINT "  $ SET HOURS       % SET MINUTES   "
15474  HTAB 3: PRINT "  ( SET MONTHS      ) SET DAYS      "
15476  HTAB 3: PRINT " ^Z CITIES UP      ^X CITIES DOWN   "
15478  HTAB 3: PRINT " ^F FIND CITY                       "
15480  NORMAL : HTAB 3: PRINT SP$: HTAB 3: PRINT SP$
15483  RETURN : REM <CTRL-J>
15485 HS = HS +1: IF HS >3  THEN HS = 1: REM <CTRL-J>
15500  REM 
15501  REM   $ VAR INIT
15502  REM 
15800  REM 
15801  REM  MSG STRS<CTRL-J>
15820 MG$(00) = "TYPE  "
15825 MG$(01) = "INSERT"
15830 MG$(02) = "                       "
15835 MG$(03) = " LINES<CTRL-J><CTRL-H><CTRL-H><CTRL-H><CTRL-H>FREE:"
15840 MG$(04) = "LABELS<CTRL-J><CTRL-H><CTRL-H><CTRL-H><CTRL-H>FREE:"
15845 MG$(05) = "ACTIVE FILE: "
15850 MG$(06) = ""
15855 MG$(07) = "WATCH FULL"
15860 MG$(08) = "ALREADY HAVE 12 LABELS<CTRL-J><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>--- UNABLE TO INSERT"
15870 MG$(10) = "WRITING DATA TO WATCH..."
15875 MG$(11) = "PRESS A NUMBER KEY"
15880 MG$(12) = "LABEL & DATA WILL BE LOST-CONT (Y/N)?"
15885 MG$(13) = ""
15890 MG$(14) = "ARE YOU SURE (Y/N)?"
15895 MG$(15) = "SAVING DATA FILE"
15900 MG$(16) = "LOADING DATA FILE"
15905 MG$(17) = "<CTRL-J>DISK ERROR - CORRECT AND RETRY"
15910 MG$(18) = "ENTER TIME<CTRL-J><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>(HH:MM:SS):"
15915 MG$(19) = "ENTER DATE<CTRL-J><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>(MM/DD/YY):"
15920 MG$(20) = "<CTRL-J>INVALID INPUT - RE-ENTER"
15925 MG$(21) = "FILE NOT SAVED! CONT (Y/N)?"
15930 MG$(22) = "PRESS <RETURN> WHEN READY"
15932 MG$(23) = "PRESS <ESC> TO EXIT"
15940 MG$(24) = "SEIKO PCDATAGRAPH WATCH DATA FILE:"
15945 MG$(25) = "ENTER<CTRL-J><CTRL-H><CTRL-H><CTRL-H><CTRL-H><CTRL-H>FILE NAME: "
15950 MG$(26) = "FILE ALREADY EXISTS-USE IT (Y/N)?"
15955 MG$(27) = "<CTRL-J>CAN'T SAVE/LOAD WITHOUT NAME"
15960 MG$(28) = " FIND CITY: "
15965 MG$(29) = "SEARCH FOR: "
15970 MG$(30) = "<CTRL-J>CAN'T LOCATE TARGET    "
15975 MG$(31) = "SEARCHING ..."
15980 MG$(34) = " ---NONE---"
15985 MG$(33) = "TIME<CTRL-J><CTRL-H><CTRL-H><CTRL-H><CTRL-H>ZONE:": REM <CTRL-J>
16000 BLANK$ = "   |       --- --- ---      |"
16010 DL$(0) = "----------- --- MEMO ---"
16020 DL$(1) = "SCHEDULE    -- ALARM ---"
16030 DL$(2) = "WEEKLY      -- ALARM ---"
16040 DL$(3) = "-----------  WORLD  TIME"
16049  REM <CTRL-J>
16050 DD$(0) = " - - - - -              "
16060 DD$(1) = " - - - - -  01/01 A12:00"
16070 DD$(2) = " - - - - -  0 SUN A12:00"
16080 DD$(3) = " - - - - -   04:00 = TZ "
16099  REM <CTRL-J>
16500  REM 
16501  REM  MENU INIT<CTRL-J>
16540  RESTORE 
16542  READ MU$: IF MU$ < >"#0"  THEN 16542
16550  FOR X = 0 TO 23
16560  READ MU$(X)
16565  NEXT X: GOTO 17180
16570  DATA  "#0": REM <CTRL-J>
16620  DATA "             MAIN MENU               "
16630  DATA "1. EDIT/CREATE WATCH DATA            "
16640  DATA "2. XMIT TO RC-1000 (LOAD WATCH)      "
16650  DATA "3. PRINT WATCH DATA                  "
16660  DATA "4. SYSTEM MENU                       "
16670  DATA "5. EXIT PROGRAM                      "
16730  REM <CTRL-J>
16760  DATA "            SYSTEM MENU              "
16770  DATA "1. SET DEFAULT WATCH DATA FILE NAME  "
16780  DATA "2. SET TIME/DATE ON COMPUTER         "
16790  DATA "3. LOAD WATCH DATA FILE FROM DISK    "
16800  DATA "4. SAVE WATCH DATA FILE TO DISK      "
16810  DATA "5. SET TIME ZONE                     "
16870  REM  <CTRL-J>
16900  DATA "            INSERT MENU              "
16910  DATA "1. ADD MEMO LABEL                    "
16920  DATA "2. ADD MEMO DATA                     "
16930  DATA "3. ADD SCHEDULE ALARM DATA/LABEL     "
16940  DATA "4. ADD WEEKLY ALARM DATA/LABEL       "
16950  DATA "5. ADD WORLD TIME DATA               "
17010  REM <CTRL-J>
17040  DATA "           LOAD WATCH                "
17050  DATA "  MAKE SURE SEIKO WRIST TERMINAL IS  "
17060  DATA " PROPERLY CONNECTED TO THE COMPUTER. "
17070  DATA "                                     "
17080  DATA "PRESS THE <RETURN> KEY TO LOAD WATCH."
17090  DATA " PRESS <ESC> TO RETURN TO MAIN MENU. "
17120  REM <CTRL-J>
17160  REM 
17161  REM  MISC STRNGS<CTRL-J>
17180 DT$(0) = "MEMO"
17190 DT$(1) = "SCHD"
17200 DT$(2) = "WKLY"
17210 DT$(3) = "WRLD"
17220 DT$(4) = "DAT: "
17230 DT$(5) = "LBL: "
17240 PR$(0) = "ME"
17250 PR$(1) = "SA"
17260 PR$(2) = "WA"
17270 PR$(3) = "WT"
17280 PR$(5) = "L: "
17290 PR$(4) = "D: "
17310 DY$(0) = "0 SUN "
17320 DY$(1) = "1 MON "
17330 DY$(2) = "2 TUE "
17340 DY$(3) = "3 WED "
17350 DY$(4) = "4 THU "
17360 DY$(5) = "5 FRI "
17370 DY$(6) = "6 SAT "
17400  RETURN 
18000  REM <CTRL-J>
19000  REM 
19001  REM   SYS CHK<CTRL-J>
19010 HI =  PEEK(115) + PEEK(116) *256
19020  IF HI +1 =  PEEK(11) + PEEK(12) *256  THEN 19040
19030  PRINT  CHR$(4);"RUN RC1000.BAS"
19040 X =  PEEK(64435):Y =  PEEK(64448)
19050  IF X = 6  AND Y = 0  THEN SYS = 3
19060  IF X  AND (Y >223  AND Y <240)  THEN SYS = 2
19070  IF X < >6  THEN SYS = 1
19080  ON SYS GOTO 19100,19100,19300
19100  HOME : VTAB 3: PRINT "APPLE II / APPLE II+ / APPLE IIE": VTAB 5: PRINT "CONNECT THE RC1000 CABLE TO THE INTERNAL16 PIN DIP PLUG GAME PORT TO LOAD WATCH.": FOR X = 1 TO 1000: NEXT :SLOT = 0
19131  RETURN : REM <CTRL-J>
19300  HOME : VTAB 3: PRINT "APPLE IIC": VTAB 5: PRINT "ENTER THE NUMBER OF THE RS232 SLOT (1-2)TO WHICH THE RC000 IS ATTACHED:<CTRL-M><CTRL-M><RETURN> OR <2> = SLOT 2 (MODEM)<CTRL-M>            <1> = SLOT 1 (PRINTER)"
19301  PRINT "<CTRL-M>RUN APPLE SYSTEM UTILITY TO SET RS232<CTRL-M>USE WARM BOOT: < ";: POKE 1024 +440,65
19302  PRINT ">-<CONTROL>-<RESET>": PRINT : PRINT "RC1000 'PIN' = 164/1111
19310  VTAB 20: HTAB 18: PRINT " ";: GET IP$
19320  IF IP$ = "2"  OR IP$ =  CHR$(13)  THEN SLOT = 2: RETURN 
19321  IF IP$ = "1"  THEN SLOT = 1: RETURN 
19339  PRINT  CHR$(7): GOTO 19310: REM <CTRL-J>
20000  END : REM --- END OF PROGRAM ---<CTRL-J><CTRL-J><CTRL-J>